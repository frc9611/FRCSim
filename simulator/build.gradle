plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    id 'java-library'

    id 'com.google.protobuf' version '0.9.4'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.60.0'
        }
    }

    generateProtoTasks {
        // all() returns the collection of all protoc tasks
        all().each { task ->
            // Here you can configure the task

            task.plugins {
                // Add grpc output without any option.  grpc must have been defined in the
                // protobuf.plugins block.
                // This yields
                // "--grpc_out=/path/to/output" on the protoc commandline.
                grpc {}
            }
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

repositories {
    jcenter()

    maven {
        url "https://frcmaven.wpi.edu/artifactory/release"
    }
}

def wpiVersion="2023.4.3"
def ejmlVersion="0.38"
def jacksonVersion="2.10.0"

dependencies {
    implementation "net.bytebuddy:byte-buddy:1.14.5"
    implementation "net.bytebuddy:byte-buddy-agent:1.14.5"

    implementation "edu.wpi.first.wpilibj:wpilibj-java:${wpiVersion}"
    implementation "edu.wpi.first.ntcore:ntcore-java:${wpiVersion}"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:${wpiVersion}"
    implementation "edu.wpi.first.wpimath:wpimath-java:${wpiVersion}"

    implementation "edu.wpi.first.cscore:cscore-java:${wpiVersion}"
    implementation "edu.wpi.first.cameraserver:cameraserver-java:${wpiVersion}"
    implementation "edu.wpi.first.hal:hal-java:${wpiVersion}"
    implementation "org.ejml:ejml-simple:${ejmlVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    // navx
    implementation "com.kauailabs.navx.frc:navx-java:3.1.400"

    implementation group: 'ch.qos.logback', name:'logback-classic', version: '1.2.3'
    implementation group: 'ch.qos.logback', name:'logback-core', version: '1.2.3'

    implementation "javax.annotation:javax.annotation-api:1.2"
    implementation 'io.grpc:grpc-all:1.60.0'


    // for config yaml
    implementation 'org.yaml:snakeyaml:1.21'
    implementation 'com.hubspot.jinjava:jinjava:2.4.15'
    implementation 'commons-io:commons-io:2.6'

    // guice dependency injection
    implementation 'com.google.inject:guice:4.2.2:no_aop'

    implementation 'org.apache.commons:commons-math3:3.6.1'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}
